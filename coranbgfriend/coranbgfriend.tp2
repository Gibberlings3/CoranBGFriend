// "Coran's BG Extended Friendship Talks"
// Version 4 Combined Tutu/BGT/BGEE installer
// Domi Sotto, updated by berelinde
// 2015-01-16
// BGEE compatibility update v4 by AstroBryGuy

// Backup folder
BACKUP "weidu_external\backup\coranbgfriend" // move backup folder outside of the main mod data folder, change only you mod data folder name

//AUTHOR ~Domi Sotto, domi@gibberlings3.net~
SUPPORT ~https://www.gibberlings3.net/forums/forum/141-coran39s-friendship-mod/~

// enable all error messages; nothing suppressed. comment this out for release version
//MODDER

VERSION ~v5.1~

README ~coranbgfriend/readme-coranbg.html~

ALWAYS
    ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
        // Tell the player it is using Tutu stuff
        PRINT @0
        // shutdown of D and BAF after BG content
        OUTER_SPRINT ~BGT_VAR~ ~~
        // Platform-Specific Filenames
        OUTER_SPRINT "CORAN_JOINED" "_CORANJ"
        OUTER_SPRINT "CORAN_BANTER" "_BCORAN"
        OUTER_SPRINT "GARRICK_BANTER" "_BGARRI"
        OUTER_SPRINT "CORAN_BCS" "_CORAN"
    END

    ACTION_IF GAME_IS ~bgt~ THEN BEGIN
        // Tell the player it is using BGT stuff
        PRINT @1
        // shutdown of D and BAF after BG content
        OUTER_SPRINT ~BGT_VAR~ ~!Global("endofbg1","GLOBAL",2)~
        // Platform-Specific Filenames
        OUTER_SPRINT "CORAN_JOINED" "CORANJ"
        OUTER_SPRINT "CORAN_BANTER" "BCORAN"
        OUTER_SPRINT "GARRICK_BANTER" "BGARRI"
        OUTER_SPRINT "CORAN_BCS" "CORAN"
    END
    
    ACTION_IF GAME_IS ~bgee~ THEN BEGIN
        // Tell the player it is using BGEE stuff
        PRINT @4
        // shutdown of D and BAF after BG content
        OUTER_SPRINT ~BGT_VAR~ ~~
        // Platform-Specific Filenames
        OUTER_SPRINT "CORAN_JOINED" "CORANJ"
        OUTER_SPRINT "CORAN_BANTER" "BCORAN"
        OUTER_SPRINT "GARRICK_BANTER" "BGARRI"
        OUTER_SPRINT "CORAN_BCS" "CORAN"
    END
    
    ACTION_IF GAME_IS ~eet~ THEN BEGIN
        // Tell the player it is using BGEE stuff
        PRINT @5
        // shutdown of D and BAF after BG content
        OUTER_SPRINT ~BGT_VAR~ ~!Global("endofbg1","GLOBAL",2)~
        // Platform-Specific Filenames
        OUTER_SPRINT "CORAN_JOINED" "CORANJ"
        OUTER_SPRINT "CORAN_BANTER" "BCORAN"
        OUTER_SPRINT "GARRICK_BANTER" "BGARRI"
        OUTER_SPRINT "CORAN_BCS" "CORAN"
    END

    ACTION_IF !(GAME_IS ~tutu tutu_totsc bgt bgee eet~) THEN BEGIN
        // Tell the player it is not Tutu, BGT, or BGEE
        FAIL @2
    END
    
    // Convert .tra file character sets to UTF-8 for BGEE
    OUTER_SPRINT MOD_FOLDER "coranbgfriend"
    MKDIR "weidu_external\lang\%MOD_FOLDER%"

    ACTION_DEFINE_ARRAY arrayNoConvert BEGIN setup END
    ACTION_DEFINE_ARRAY arrayReload BEGIN p#coranft END

    LAF HANDLE_CHARSETS
        INT_VAR
            from_utf8 = 1
            infer_charset = 1
            verbose = 1
        STR_VAR
            default_language = english
            tra_path = EVAL "%MOD_FOLDER%/lang"
            out_path = EVAL "weidu_external/lang/%MOD_FOLDER%"
            noconvert_array = arrayNoConvert
            reload_array = arrayReload
    END

END

// Language Settings
LANGUAGE ~English~ ~english~ ~coranbgfriend/lang/english/setup.tra~
LANGUAGE ~Deutsch~ ~german~ ~coranbgfriend/lang/german/setup.tra~
LANGUAGE ~Russian~ ~russian~ ~coranbgfriend/lang/english/setup.tra~

BEGIN @3 LABEL "Domi-CoranBGFriend-Main"
    
    // Tells other mods BG1NPC Core is installed
    COPY ~coranbgfriend/p#component.xx~ ~override/p#coranfriend.g3~

    // STATE.IDS patching to ToB, courtesy CamDawg
    // adds custom IsValidForPartyDialogue state
    APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

    // dialogue
    COMPILE EVALUATE_BUFFER ~coranbgfriend/dlg/p#coranft.d~ USING ~coranbgfriend/lang/%s/P#CORANFT.tra~

    // scripts
    EXTEND_TOP ~%CORAN_BCS%.bcs~ ~coranbgfriend/baf/p#coranbgfriendship.baf~
        EVALUATE_BUFFER

    ACTION_IF NOT FILE_EXISTS_IN_GAME ~x#bg1npccore.g3~ THEN BEGIN
        EXTEND_TOP ~%CORAN_BCS%.bcs~ ~coranbgfriend/baf/p#corannobg1npc.baf~
        EVALUATE_BUFFER
    END

    // cutscenes
    COMPILE ~coranbgfriend/baf/p#coweek.baf~
    COMPILE ~coranbgfriend/baf/p#cowee1.baf~
